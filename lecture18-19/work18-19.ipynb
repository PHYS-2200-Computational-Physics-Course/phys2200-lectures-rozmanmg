{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b32d510b-e3ae-4566-a68b-922396479508",
   "metadata": {},
   "source": [
    "## White dwarfs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ac658be0-96a8-4a6c-81ad-58ccdc8030ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "url = \"https://cdsarc.u-strasbg.fr/viz-bin/asu-tsv?-source=J/A+A/420/507/tablea1&-out=Mass&-out=Rad\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c82924e4-4433-4892-b9b2-e48aea78e81d",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "catalog = download(url)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8bdc59b4-4887-4b92-b63b-0e45c4bf0fa5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using CSV\n",
    "using DataFrames"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "08ff296f-b71d-46f8-81d2-04f5eda910ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "df = CSV.read(catalog, DataFrame, comment=\"#\", skipto=4, \n",
    "              types=[Float64, Float64], delim='\\t', ignorerepeated=true, \n",
    "              silencewarnings=true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1cab000f-c2a6-4b70-b0c4-d623dcb9c0ef",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "dropmissing!(df)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "de4e65f8-1c3c-4aa6-bfce-7bc2cedaff49",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using PyPlot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "df4a7539-24f4-4f3c-b217-4525235794ae",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(df.Mass, df.Rad, marker=\"o\", linestyle=\"none\", \n",
    "     fillstyle=\"none\", markersize=4, label=\"observations\")\n",
    "grid(true)\n",
    "xlabel(L\"$M/M_{\\odot}$\")\n",
    "ylabel(L\"$R/R_{\\odot}$\")\n",
    "legend()\n",
    "title(\"Observational data for white dwarf stars\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "de414d9f-63a9-4667-88cc-1de91fe9a930",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using OrdinaryDiffEqTsit5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c25adcbf-b97d-4c48-84b5-5e7b6e579526",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "    white_dwarf_eqs!(dudr, u, p, r)\n",
    "\n",
    "The right hand side of the system of 2 dimensionless differential \n",
    "equations describing the radial distribution of the density, rho(r),\n",
    "and mass, m(r), inside a white dwarf star\n",
    "\n",
    "     m' =  rho r^2\n",
    "   rho' = -m rho /(gamma(\\rho) r^2)\n",
    "\n",
    "where gamma(rho) = rho^(2/3)/(3 sqrt(1 + rho^(2/3)))\n",
    "\"\"\"\n",
    "function white_dwarf_eqs!(dudr, u, p, r)\n",
    "    m = u[1]\n",
    "    rho = u[2]\n",
    "    rho_c = p\n",
    "    if rho >= 0\n",
    "        w = rho^(2/3)\n",
    "        gamma = w/(3*sqrt(1 + w))\n",
    "        dudr[1] = rho * r^2\n",
    "        if r > 1.0e-6\n",
    "            dudr[2] = - m *rho/(r^2 * gamma)\n",
    "        else\n",
    "            dudr[2] = - rho_c * r * rho /(3 * gamma)\n",
    "        end\n",
    "    else\n",
    "        dudr[1] = 0.0\n",
    "        dudr[2] = 0.0\n",
    "    end\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f69bf53a-9da1-4a0b-8dae-94812a14e214",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rho_c1 = 0.08      #  min density in the star center\n",
    "rho_c2 = 100000.0  #  max density in the star center\n",
    "np = 100\n",
    "\n",
    "rhos = exp.(range(log(rho_c1), log(rho_c2), np))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71c1892d-50aa-4950-bf7e-9596afa1e6fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "radi = zeros(np)\n",
    "mass = zeros(np);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "81cf63b8-e819-48b5-8f1c-c8c133f4a1e6",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b5532779-cd0c-4679-8acb-ac39d4b47436",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "for i = 1:np\n",
    "\n",
    "    rspan = (0.0, 10.0)\n",
    "    rho_c = rhos[i]\n",
    "    u0 = [0.0, rho_c]\n",
    "    prob = ODEProblem(white_dwarf_eqs!, u0, rspan, rho_c)\n",
    "\n",
    "    # Set up the callback\n",
    "    condition(u, t, integrator) = u[2]  # stop integration when rho = 0.\n",
    "    affect!(integrator) = terminate!(integrator)\n",
    "    cb = ContinuousCallback(condition, affect!)\n",
    "\n",
    "    sol = solve(prob, Tsit5(), callback=cb)\n",
    "    \n",
    "    radi[i] = sol.t[end]\n",
    "    mass[i] = sol.u[end][1]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d769cb3d-4981-4716-aebd-91d2b2fc20b5",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(mass * 0.71, radi * 0.006, marker=\".\", label=\"theory\")\n",
    "plot(df.Mass, df.Rad, marker=\"o\", linestyle=\"none\", \n",
    "     fillstyle=\"none\", markersize=4, label=\"observations\")\n",
    "grid(true)\n",
    "xlabel(L\"$M/M_{\\odot}$\")\n",
    "ylabel(L\"$R/R_{\\odot}$\")\n",
    "legend()\n",
    "title(\"Observational data for white dwarf stars\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c18be81f-8c3e-457a-9b05-fa1df7c67aa6",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.6",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
